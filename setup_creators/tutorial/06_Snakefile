# our samples are pre-configured
SAMPLES = ["A", "B"]

# Task: Which is our second target file?
#       Add it to the input of the 'all' rule.
#       Attention: What do you need to do, to
#       keep the syntax right?
#
#       Subsequently run the workflow - if
#       there is an error (e.g. a typo from 
#       copying the slides), run Snakemake
#       with the '--debug' flag.

rule all:
    input:
        "calls/all.vcf"

rule bwa_map:
    input:
        "data/genome.fa",
        "data/samples/{sample}.fastq"
    output:
        "mapped_reads/{sample}.bam"
    shell:
        "bwa mem {input} | samtools view -Sb - > {output}"


rule samtools_sort:
    input:
        "mapped_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam"
    shell:
        "samtools sort -T sorted_reads/{wildcards.sample} "
        "-O bam {input} > {output}"


rule samtools_index:
    input:
        "sorted_reads/{sample}.bam"
    output:
        "sorted_reads/{sample}.bam.bai"
    shell:
        "samtools index {input}"


rule bcftools_call:
    input:
        fa="data/genome.fa",
        bam=expand("sorted_reads/{sample}.bam", sample=SAMPLES),
        bai=expand("sorted_reads/{sample}.bam.bai", sample=SAMPLES)
    output:
        "calls/all.vcf"
    shell:
        "bcftools mpileup -f {input.fa} {input.bam} | "
        "bcftools call -mv - > {output}"

rule plot_quals:
    input:
       "calls/all.vcf"
    output:
       "plots/quals.svg"
    script:
       "scripts/plot-quals.py"


